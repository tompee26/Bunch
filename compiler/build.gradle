apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    generated {
        java {
            srcDirs "${buildDir}/generated/source/kapt/"
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def toolJarDir = properties.getProperty('tooljar.dir')

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.groupProjectID
            artifactId = 'compiler'
            version = project.publishVersion
            from components.java

            pom {
                name = project.projectName
                description = 'Compiler package for Bunch'
                url = 'https://github.com/tompee26/Bunch'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/tompee26/Bunch/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'tompee26'
                        name = 'tompee'
                        email = 'tompee26@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/tompee26/Bunch")
            credentials {
                username = "tompee26"
                password = properties.getProperty("token")
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation project(':annotation')
    implementation files("${toolJarDir}/lib/tools.jar")

    implementation "com.google.auto.service:auto-service:1.0-rc6"
    kapt "com.google.auto.service:auto-service:1.0-rc6"

    def incap = "0.2"
    compileOnly "net.ltgt.gradle.incap:incap:$incap"
    kapt "net.ltgt.gradle.incap:incap-processor:$incap"

    def kotlinPoet = "1.8.0"
    implementation "com.squareup:kotlinpoet:$kotlinPoet"
    implementation "com.squareup:kotlinpoet-metadata:$kotlinPoet"
    implementation "com.squareup:kotlinpoet-metadata-specs:$kotlinPoet"
    implementation "com.squareup:kotlinpoet-classinspector-elements:$kotlinPoet"
}